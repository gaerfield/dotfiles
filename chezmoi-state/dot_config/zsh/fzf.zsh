# Setup fzf
# ---------
FZF_REPO="${ZINIT[PLUGINS_DIR]}/junegunn---fzf"
FZF_BINPATH="$ZPFX/bin/"
FZF_MANPATH="$FZF_REPO/man"

if [[ ! "$PATH" =~ "$FZF_BINPATH" ]]; then
  export PATH="$PATH:$FZF_BINPATH"
fi

# Man path
# --------
if [[ ! "$MANPATH" =~ "$FZF_MANPATH" && -d "$FZF_MANPATH" ]]; then
  export MANPATH="$MANPATH:$FZF_MANPATH"
fi

# Auto-completion
# ---------------
[[ $- =~ i ]] && source "$FZF_REPO/shell/completion.zsh"

# Key bindings
# ------------
source "$FZF_REPO/shell/key-bindings.zsh"

export FZF_DEFAULT_OPTS='
  --color fg:-1,bg:-1,hl:33,fg+:254,bg+:235,hl+:33
  --color info:136,prompt:136,pointer:230,marker:230,spinner:136
'

export FZF_CTRL_T_COMMAND='rg --files --no-messages'
export FZF_CTRL_T_OPTS="--preview '(highlight -O ansi -l {} 2> /dev/null || cat {} || tree -C {}) 2> /dev/null | head -200'"
export FZF_CTRL_R_OPTS='--sort'
export FZF_ALT_C_OPTS="--preview 'tree -C {} | head -200'"

# git
# ---

# GIT + FZF
# https://junegunn.kr/2016/07/fzf-git/
# -------------
is_in_git_repo() {
  git rev-parse HEAD > /dev/null 2>&1
}

fzf-down() {
  fzf --height 50% "$@" --border
}

gf() {
  is_in_git_repo || return
  git -c color.status=always status --short |
  fzf-down -m --ansi --nth 2..,.. \
    --preview '(git diff --color=always -- {-1} | sed 1,4d; cat {-1}) | head -500' |
  cut -c4- | sed 's/.* -> //'
}

gb() {
  is_in_git_repo || return
  git branch -a --color=always | grep -v '/HEAD\s' | sort |
  fzf-down --ansi --multi --tac --preview-window right:70% \
    --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" $(sed s/^..// <<< {} | cut -d" " -f1) | head -'$LINES |
  sed 's/^..//' | cut -d' ' -f1 |
  sed 's#^remotes/##'
}

gt() {
  is_in_git_repo || return
  git tag --sort -version:refname |
  fzf-down --multi --preview-window right:70% \
    --preview 'git show --color=always {} | head -'$LINES
}

gh() {
  is_in_git_repo || return
  git log --date=short --format="%C(green)%C(bold)%cd %C(auto)%h%d %s (%an)" --graph --color=always |
  fzf-down --ansi --no-sort --reverse --multi --bind 'ctrl-s:toggle-sort' \
    --header 'Press CTRL-S to toggle sort' \
    --preview 'grep -o "[a-f0-9]\{7,\}" <<< {} | xargs git show --color=always | head -'$LINES |
  grep -o "[a-f0-9]\{7,\}"
}

gr() {
  is_in_git_repo || return
  git remote -v | awk '{print $1 "\t" $2}' | uniq |
  fzf-down --tac \
    --preview 'git log --oneline --graph --date=short --pretty="format:%C(auto)%cd %h%d %s" {1} | head -200' |
  cut -d$'\t' -f1
}

join-lines() {
  local item
  while read item; do
    echo -n "${(q)item} "
  done
}

bind-git-helper() {
  local c
  for c in $@; do
    eval "fzf-g$c-widget() { local result=\$(g$c | join-lines); zle reset-prompt; LBUFFER+=\$result }"
    eval "zle -N fzf-g$c-widget"
    eval "bindkey '^g^$c' fzf-g$c-widget"
  done
}
bind-git-helper f b t r h
unset -f bind-git-helper

# nÃ¤her untersuchen: https://github.com/junegunn/fzf/wiki/examples#git

# mehr fzf: https://github.com/junegunn/fzf/wiki/Examples
